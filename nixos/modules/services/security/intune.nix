{ config
, pkgs
, lib
, ...
}:
with lib; let
  cfg = config.services.intune;
in
{
  options.services.intune = {
    enable = mkEnableOption (lib.mdDoc "Microsoft Intune");
  };


  config = mkIf cfg.enable {
    users.users.microsoft-identity-broker = {
      group = "microsoft-identity-broker";
      isSystemUser = true;
    };

    users.groups.microsoft-identity-broker = { };
    environment.systemPackages = [ pkgs.microsoft-identity-broker pkgs.intune-portal ];
    systemd.packages = [ pkgs.microsoft-identity-broker ];

    systemd.services.microsoft-identity-device-broker.enable = true;
    systemd.services.microsoft-identity-device-broker.serviceConfig.ExecStartPre = "";
    systemd.user.services.microsoft-identity-broker.enable = true;
    systemd.user.services.microsoft-identity-broker.serviceConfig.ExecStartPre = "";


    systemd.tmpfiles.packages = [ pkgs.intune-portal ];
    # Only really want the wants file set, but haven't been able to figure out how to do what without setting the whole thing here
    systemd.user.timers.intune-agent = {
      enable = true;
      description = "Intune Agent scheduler";
      after = [ "graphical-session.target" ];
      partOf = [ "graphical-session.target" ];
      unitConfig = {
        DefaultDependencies = "no";
      };
      timerConfig = {
        AccuracySec = "2m";
        OnStartupSec = "5m";
        OnUnitActiveSec = "1h";
        RandomizedDelaySec = "10m";
      };
      wantedBy = [ "graphical-session.target" ];
    };

    services.dbus.packages = [ pkgs.microsoft-identity-broker ];
  };

  meta = {
    maintainers = with maintainers; [ rhysmdnz ];
  };
}
